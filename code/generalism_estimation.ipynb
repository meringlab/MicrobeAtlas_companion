{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "e58f3fdc-d790-4cbe-bb6f-d9342ad595fa",
   "metadata": {},
   "source": [
    "Note: all code is written in julia (https://julialang.org/) and should be compatible with julia v1.9 and later versions.\n",
    "\n",
    "Code author: Janko Tackmann (jtackm@github)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "c22f5821-72e8-4f9c-b673-3d198d8ea81f",
   "metadata": {},
   "outputs": [],
   "source": [
    "###########################################################################\n",
    "# Functions to aggregate abundances and compute habitat generalism scores #\n",
    "###########################################################################\n",
    "\n",
    "using Statistics, StatsBase, SparseArrays, AxisArrays, ProgressMeter\n",
    "\n",
    "function make_sgabund_matrix(sg_oabund_dict, sg_header, oid_header)\n",
    "    n_sg = length(sg_header)\n",
    "    n_oid = length(oid_header)\n",
    "    A = zeros(Float32, n_sg, n_oid)\n",
    "    oid_header_rev = Dict(oid=>i for (i, oid) in enumerate(oid_header))\n",
    "    @showprogress for (i, sg) in enumerate(sg_header)\n",
    "        for (oid, abund) in sg_oabund_dict[sg]\n",
    "            if haskey(oid_header_rev, oid)\n",
    "                oid_j = oid_header_rev[oid]\n",
    "                A[i, oid_j] = abund\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "    AxisArray(A, sg_header, oid_header)\n",
    "end\n",
    "\n",
    "function sum_abunds_per_otu_and_sgroup(otu_matn_T::AxisArray{T}, sgroup_map::Dict) where T<:Real\n",
    "    n_samps = size(otu_matn_T, 2)\n",
    "    rev_sid_map = Dict(s=>i for (i, s) in enumerate(axisvalues(otu_matn_T)[2]))\n",
    "    rev_sg_map = Dict(s=>i for (i, s) in enumerate(keys(sgroup_map)))\n",
    "    sg_idx = zeros(Int, n_samps)\n",
    "    \n",
    "    for (sg, sids) in sgroup_map\n",
    "        sg_i = rev_sg_map[sg]\n",
    "        for sid in sids\n",
    "            if haskey(rev_sid_map, sid)\n",
    "                sg_idx[rev_sid_map[sid]] = sg_i\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "    \n",
    "    @assert !any(iszero.(sg_idx))\n",
    "    \n",
    "    accum_dict = Dict{Int,Dict{Int,T}}()\n",
    "    A = otu_matn_T.data\n",
    "    nzv = nonzeros(A)\n",
    "    rvs = rowvals(A)\n",
    "    \n",
    "    @showprogress for s_i in 1:n_samps\n",
    "        s_itr = nzrange(A, s_i)\n",
    "        \n",
    "        sg_i = sg_idx[s_i]\n",
    "        if !haskey(accum_dict, sg_i)\n",
    "            sub_acc_dict = Dict(rvs[j] => nzv[j] for j in s_itr)\n",
    "            accum_dict[sg_i] = sub_acc_dict\n",
    "        else\n",
    "            sub_acc_dict = accum_dict[sg_i]\n",
    "            for j in s_itr\n",
    "                oid = rvs[j]\n",
    "                abund = nzv[j]\n",
    "                if haskey(sub_acc_dict, oid)\n",
    "                    sub_acc_dict[oid] += abund\n",
    "                else\n",
    "                    sub_acc_dict[oid] = abund\n",
    "                end\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "    sg_map = Dict(v=>k for (k,v) in rev_sg_map)\n",
    "    oid_map = axisvalues(otu_matn_T)[1]\n",
    "    @time trans_accum_dict = Dict(sg_map[sg_i]=>Dict(oid_map[oid_i]=>abund for (oid_i, abund) in sub_d)\n",
    "                            for (sg_i, sub_d) in accum_dict)\n",
    "    trans_accum_dict\n",
    "end\n",
    "\n",
    "function convert_to_mean_abunds(accum_dict, sgroup_map; nz=false, prev_accum_dict=nothing)\n",
    "    @assert !nz || prev_accum_dict != nothing \"provide a prevalence count dict when choosing 'nz=true'\"\n",
    "    mean_accum_dict = Dict{String,Dict{String,Float64}}()\n",
    "    @showprogress for (sg, sub_acc_dict) in accum_dict\n",
    "        if !nz\n",
    "            sg_size = length(sgroup_map[sg])\n",
    "            mean_accum_dict[sg] = Dict(oid=>abund/sg_size for (oid, abund) in sub_acc_dict)\n",
    "        else\n",
    "            sub_prev_dict = prev_accum_dict[sg]\n",
    "            mean_accum_dict[sg] = Dict(oid=>abund/sub_prev_dict[oid] for (oid, abund) in sub_acc_dict)\n",
    "        end\n",
    "    end\n",
    "    mean_accum_dict\n",
    "end\n",
    "\n",
    "function mean_abunds_per_habitat(A_sg_mean_abunds::AxisArray, env_map::AbstractVector)\n",
    "    @assert size(A_sg_mean_abunds, 1) == length(env_map)\n",
    "    rows = []\n",
    "    envs = unique(env_map)\n",
    "    for env in envs\n",
    "        env_mask = env_map .== env\n",
    "        A_env = A_sg_mean_abunds.data[env_mask, :]\n",
    "        push!(rows, mean(A_env, dims=1))\n",
    "    end\n",
    "    AxisArray(vcat(rows...), envs, axisvalues(A_sg_mean_abunds)[2])\n",
    "end\n",
    "\n",
    "function env_entropy_generalism(mean_abunds::AbstractVector)\n",
    "    mean_abunds_norm = mean_abunds ./ sum(mean_abunds)\n",
    "    entropy(mean_abunds_norm)\n",
    "end\n",
    "\n",
    "function env_entropy_generalism(A_sg_mean_abunds::AxisArray, env_map::AbstractVector)\n",
    "    @assert size(A_sg_mean_abunds, 1) == length(env_map)\n",
    "    A_env_abund = mean_abunds_per_habitat(A_sg_mean_abunds, env_map)\n",
    "    gen_scores = mapslices(env_entropy_generalism, A_env_abund.data, dims=1)\n",
    "    (gen_scores=AxisArray(vec(gen_scores), axisvalues(A_env_abund)[2]), env_abunds=A_env_abund)\n",
    "end"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.5",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
